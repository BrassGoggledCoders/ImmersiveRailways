buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT"
    }
}

apply plugin: "net.minecraftforge.gradle.forge"

version = modVersion
group = modGroup
archivesBaseName = modBaseName

minecraft {
    version = project.minecraftVersion + "-" + project.forgeVersion
    runDir = "run"
    mappings = project.mcpVersion

    replace "@VERSION@", project.version
    replaceIn "VehicularEngineering.java.java"

    replace "base@[0.0.0,)", "base@[" + baseVersion + ",)"
    replaceIn "VehicularEngineering.java"
}

repositories {
    mavenCentral()
    maven {
        name "Base/IE"
        url "http://maven.blamejared.com"
    }
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

dependencies {
    deobfCompile "com.teamacronymcoders.base:base:${minecraftVersion}-${baseVersion}-Snapshot.+"
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"

        // replace version and mcversion
        expand "version": project.version, "mcversion": project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}

apply plugin: 'maven'
uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "file://" + (System.getenv("local_maven") != null ? System.getenv("local_maven") : System.getenv("bamboo_local_maven")))
            pom {
                groupId = project.group
                version = project.version
                artifactId = project.archivesBaseName
                project {
                    name project.archivesBaseName
                    packaging 'jar'
                    description project.archivesBaseName
                    url ''
                    scm {
                        url 'https://github.com/BrassGoggledCoders/VehicularEngineering'
                        connection 'scm:git:git@github.com:BrassGoggledCoders/VehicularEngineering.git'
                        developerConnection 'scm:git:git@github.com:BrassGoggledCoders/VehicularEngineering.git'
                    }
                    issueManagement {
                        system 'github'
                        url 'https://github.com/BrassGoggledCoders/VehicularEngineering/issues'
                    }
                    licenses {
                        license {
                            name 'MIT'
                            url 'https://github.com/BrassGoggledCoders/VehicularEngineering/blob/master/LICENSE'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id 'SkySom'
                            name 'SkySom'
                            roles { role 'developer' }
                        }
                    }
                }
            }
        }
    }
}
